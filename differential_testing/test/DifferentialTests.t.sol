pragma solidity 0.8.13;

import "../../src/Merkle.sol";
import "forge-std/Test.sol";
import "openzeppelin-contracts/contracts/utils/cryptography/MerkleProof.sol";
import "openzeppelin-contracts/contracts/utils/Strings.sol";
import "forge-std/console.sol";

contract DifferentialTests is Test {

    // Contracts (to be migrated to libraries)
    Merkle m;
    bytes32[100] data;

    function setUp() public {
        m = new Merkle();
    }
    
    function testMerkleRootMatchesJSImplementation() public {
        // Run the reference implementation in javascript
        string[] memory runJsInputs = new string[](6);
        runJsInputs[0] = 'npm';
        runJsInputs[1] = '--prefix';
        runJsInputs[2] = 'differential_testing/scripts/';
        runJsInputs[3] = '--silent';
        runJsInputs[4] = 'run';
        runJsInputs[5] = 'generate-root';
        bytes memory jsResult = vm.ffi(runJsInputs);
        bytes32 jsGeneratedRoot = abi.decode(jsResult, (bytes32));

        // Read in the file generated by the reference implementation 
        string[] memory loadJsDataInputs = new string[](2);
        loadJsDataInputs[0] = "cat";
        loadJsDataInputs[1] = "differential_testing/data/input";
        bytes memory loadResult =  vm.ffi(loadJsDataInputs);
        data  = abi.decode(loadResult, (bytes32[100]));

        // Calculate root using Murky
        bytes32 murkyGeneratedRoot = m.getRoot(_getData());
        assertEq(murkyGeneratedRoot, jsGeneratedRoot);
    }

    
    function testMerkleRootMatchesJSImplementationFuzzed(bytes32[] memory leafs) public {
        vm.assume(leafs.length > 1);
        // this will be fuzzed, just static for testing
        
        // bytes32[] memory leafs = new bytes32[](6); 
        // leafs[0] = bytes32(uint256(0xdead));
        // leafs[1] = bytes32(uint256(0xface));
        // leafs[2] = bytes32(uint256(0xbabe));
        // leafs[3] = bytes32(uint256(0x1337));
        // leafs[4] = bytes32(uint256(0x1234));
        // leafs[5] = bytes32(uint256(0xbeef));

        bytes memory packed = abi.encodePacked(leafs);
        string memory jsArg = bytesToHexString(packed);
        emit log_string(jsArg);
        string[] memory runJsInputs = new string[](8);
        runJsInputs[0] = 'npm';
        runJsInputs[1] = '--prefix';
        runJsInputs[2] = 'differential_testing/scripts/';
        runJsInputs[3] = '--silent';
        runJsInputs[4] = 'run';
        runJsInputs[5] = 'generate-root-stdin';
        runJsInputs[6] = Strings.toString(leafs.length);
        runJsInputs[7] = jsArg;
        bytes memory jsResult = vm.ffi(runJsInputs);
        bytes32 jsGeneratedRoot = abi.decode(jsResult, (bytes32));
        emit log_bytes32(jsGeneratedRoot);
        
        // Calculate root using Murky
        bytes32 murkyGeneratedRoot = m.getRoot(leafs);
        emit log_bytes32(murkyGeneratedRoot);
        assertEq(murkyGeneratedRoot, jsGeneratedRoot);
    }

    function testCompatabilityOpenZeppelinProver(bytes32[] memory _data, uint256 node) public {
        vm.assume(_data.length > 1);
        vm.assume(node < _data.length);
        bytes32 root = m.getRoot(_data);
        bytes32[] memory proof = m.getProof(_data, node);
        bytes32 valueToProve = _data[node];
        bool murkyVerified = m.verifyProof(root, proof, valueToProve);
        bool ozVerified = MerkleProof.verify(proof, root, valueToProve);
        assertTrue(murkyVerified == ozVerified);
    }

    function _getData() public view returns (bytes32[] memory) {
        bytes32[] memory _data = new bytes32[](data.length);
        uint length = data.length;
        for (uint i = 0; i < length; ++i) {
            _data[i] = data[i];
        }
        return _data;
    }

    function bytesToHexString(bytes memory input) public returns (string memory) {
        bytes16 symbols = "0123456789abcdef";
        bytes memory hex_buffer = new bytes(2 * input.length + 2);
        hex_buffer[0] = "0";
        hex_buffer[1] = "x";

        uint pos = 2;
        uint256 length = input.length;
        for (uint i = 0; i < length; ++i) {
            uint _byte = uint8(input[i]);
            hex_buffer[pos++] = symbols[_byte >> 4 & 0xf];
            hex_buffer[pos++] = symbols[_byte & 0xf];
        }
        return string(hex_buffer);
    }
}